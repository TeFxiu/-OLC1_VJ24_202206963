package Analizadores;


//importaciones
import java.util.HashMap;
import instrucciones.Start;
import java_cup.runtime.Symbol;
import abstracto.Instruccion;
import simbolo.Tipo;
import simbolo.TipoDato;
import instrucciones.IF;
import instrucciones.Else;
import instrucciones.Match;
import expresiones.Caso;
import instrucciones.ForC;
import instrucciones.WhileC;
//instrucciones
import instrucciones.Print;
import instrucciones.AsignacionVar;
import instrucciones.OperacionUnitaria;
import instrucciones.List;
import expresiones.RestList;

import expresiones.Nativo;
import expresiones.Aritmeticas;
import expresiones.OperadoresAritmeticos;
import expresiones.OperadoresRelacionales;
import expresiones.Relacionales;
import expresiones.Logicos;
import expresiones.OperadoresLogicos;
import java.util.LinkedList;
import excepciones.ErrorS;
import expresiones.Casteo;
import instrucciones.Declaracion;
import expresiones.AccesoVar;
import expresiones.TipoMutable;
import instrucciones.Dow;
import instrucciones.Fin;
import instrucciones.Vector;
import expresiones.AccesoVec;
import instrucciones.Metodo;
import instrucciones.AsignarVec;
import simbolo.TipoEDD;
import instrucciones.ApenList;
import instrucciones.Retorno;
import instrucciones.Struct;

// parser
parser code
{:
    scanner s;
    parser(scanner s){this.s = s;}

    public LinkedList<ErrorS> listaErrores = new LinkedList<>();
    
    public void syntax_error(Symbol s){
        listaErrores.add(new ErrorS("Sintactico R", "No se esperaba el componente"+s.value, s.left, s.right));
        System.out.println("Error Sintactico en la linea " +
        (s.left) + " y columna " + (s.right) +
        ". No se esperaba el componente: " + (s.value) + ".");
    }

    public void unrecovered_syntax_error (Symbol s){
        listaErrores.add(new ErrorS("Sintactico  NR", "No se esperaba el componente"+s.value, s.left, s.right));
        System.out.println("Error Sintactico no recuperable en la linea" +
        (s.left) + " y columna " + (s.right) +
        ". No se esperaba el componente: " + (s.value) + ".");
    }

:}

action code
{:
:}

//terminales
terminal String CADENA, ENTERO, DECIMAL, BOOL, CARACTER, ID;
terminal FINCADENA, IMPRIMIR, VAR, CONST;
terminal DOS_PUNTOS, IGUAL;
terminal PAR_A, PAR_C, MAS, MENOS, UMENOS, MULT, MODULO, POTENCIA,DIV;
terminal IGUALACION, DIFERENCIA, MAYOR_IGUAL, MENOR_IGUAL;
terminal MENOR, MAYOR;
terminal OR, AND, XOR,NOT;
terminal INCREMENTO, DECREMENTO;
terminal INT, DOUBLE, CHAR, BOOLEAN, STRING;
terminal IF,ELSE, LLAVE_A, LLAVE_C;
terminal FORC, WHILEC, DO_C;
terminal FIN, CONTINUAR;
terminal DEFAULT, FLECHA, MATCH;
terminal BRACKET_A, BRACKET_C, BRACKETS, COMA;
terminal TVOID;
terminal LISTA, NUEVO;
terminal DOTT, APEL;
terminal REST;
terminal START, RETORNO;
terminal STRUCT;

//no terminales
nonterminal LinkedList<Instruccion> INICIO, INSTRUCCIONES;
nonterminal Instruccion INSTRUCCION;
nonterminal Instruccion EXPRESION;
nonterminal Instruccion SENT_IF, BUCLE_IF;
nonterminal Instruccion IMPRESION, DECLARACION, ASIGNACION, OPERACIONU;
nonterminal TipoMutable MUTABILIDAD;
nonterminal TipoDato TIPO;
nonterminal Instruccion FORB,ACTU_FOR, ASIG;
nonterminal Instruccion CWHILE, DOWHILE;
nonterminal Instruccion FINB;
nonterminal Instruccion CMATCH;
nonterminal LinkedList<Caso> CASOS;
nonterminal Instruccion VECTORES, ASIGNACIONV; 
nonterminal LinkedList<Instruccion> VALORES, BRACK;
nonterminal LinkedList<LinkedList> BRACK2D;
nonterminal Instruccion METODO;
nonterminal LinkedList<HashMap> PARAMETROS;
nonterminal LinkedList<HashMap> LISTP;
nonterminal Instruccion LIST;
nonterminal Instruccion ANIADIR;
nonterminal Instruccion SMAIN;
nonterminal Instruccion LLAMADA;
nonterminal LinkedList<Instruccion> PARLLAMADA;
nonterminal Instruccion REGRESO;
nonterminal Instruccion STR;
nonterminal HashMap <String, String>LISTR;

precedence left OR;
precedence left AND;
precedence left XOR;
precedence right NOT;
precedence left IGUALACION, DIFERENCIA, MENOR, MENOR_IGUAL, MAYOR, MAYOR_IGUAL;
precedence left MAS, MENOS;
precedence left MULT, DIV, MODULO;
precedence nonassoc POTENCIA;
precedence left UMENOS;

start with INICIO;

INICIO ::= INSTRUCCIONES:a      {:   RESULT = a;   :}
;

INSTRUCCIONES ::= INSTRUCCIONES:a INSTRUCCION:b     {: RESULT = a; RESULT.add(b); :}
                | INSTRUCCION:a                     {: RESULT = new LinkedList<>(); RESULT.add(a); :}
;

INSTRUCCION ::=  IMPRESION:a        {:      RESULT = a;   :}
                |DECLARACION:a      {:      RESULT = a;   :}
                |ASIGNACION:a       {:      RESULT = a;   :}
                |ASIGNACIONV:a      {:      RESULT = a; :}
                |OPERACIONU:a       {:      RESULT = a;   :}
                |SENT_IF:a          {:      RESULT = a;   :}
                |FORB:a             {:      RESULT = a; :}
                |CWHILE:a           {:      RESULT = a; :}
                |DOWHILE:a          {:      RESULT = a; :}
                |FINB:a             {:      RESULT = a; :}
                |CMATCH:a           {:      RESULT = a; :}
                |VECTORES:a         {:      RESULT =a;  :}
                |METODO:a           {:      RESULT =a;:}
                |ANIADIR:a          {:      RESULT =a;:}
                |LIST:a             {:      RESULT =a; :}
                |REGRESO:a          {:      RESULT =a; :}
                |LLAMADA:a FINCADENA           {:   RESULT = a; :}
                |STR:a                  {: RESULT =a; :}
                |SMAIN:a            {:      RESULT =a; :}
                | error FINCADENA
;

STR ::= STRUCT LLAVE_A LISTR:a LLAVE_C ID:b FINCADENA   {: RESULT = new Struct(a, b, aleft, aright); :}
;

LISTR ::= LISTR:a ID:b DOS_PUNTOS ID:c FINCADENA         {: RESULT =a; a.put(b.toLowerCase(), c.toLowerCase()); :}
            |ID:a DOS_PUNTOS ID:b FINCADENA         {: RESULT = new HashMap<String, String>();
                                                    RESULT.put(a.toLowerCase(), b.toLowerCase());  :}
;

METODO ::= TIPO:d ID:a PARAMETROS:b LLAVE_A INSTRUCCIONES:c LLAVE_C {: RESULT = new Metodo(a,b,c, new Tipo(d),aleft, aright); :}
;

PARAMETROS ::=  PAR_A PAR_C            {: RESULT = new LinkedList<HashMap>(); :}
                |PAR_A LISTP:a PAR_C      {: RESULT =a;  :}
;

LISTP ::= LISTP:a COMA TIPO:b ID:c      {: RESULT =a; 
                                        var hm2 = new HashMap<String, Object>(); 
                                        hm2.put("id", c); 
                                        hm2.put("tipo", new Tipo(b)); RESULT.add(hm2); :}
        |TIPO:a ID:b                    {: RESULT = new LinkedList<HashMap>(); 
                                        var hm = new HashMap<String, Object>(); 
                                        hm.put("id", b); 
                                        hm.put("tipo", new Tipo(a)); RESULT.add(hm);:}
;

SMAIN ::=   START LLAMADA:a FINCADENA           {: RESULT = a;:} 
;

LLAMADA ::=     ID:a PAR_A PARLLAMADA:b PAR_C      {: RESULT = new Start(a,b, aleft, aright);:}
                |ID:a PAR_A PAR_C                  {: RESULT = new Start(a, new LinkedList<>(), aleft, aright); :}
            
;

PARLLAMADA ::= PARLLAMADA:a COMA EXPRESION:b    {:RESULT = a; RESULT.add(b);:} 
            |EXPRESION:a                        {: RESULT = new LinkedList<>(); RESULT.add(a);:}
;


REGRESO ::= RETORNO EXPRESION:a FINCADENA       {: RESULT = new Retorno(a, aleft,aright); :}
            |RETORNO FINCADENA:a                  {: RESULT = new Retorno(null,aleft, aright ); :}
;

LIST ::= LISTA MENOR TIPO:a MAYOR ID:b IGUAL NUEVO LISTA PAR_A PAR_C FINCADENA          {: RESULT = new List(b, new Tipo(a), aleft,aright); :}
;

VECTORES ::= MUTABILIDAD:a ID:b DOS_PUNTOS TIPO:c BRACKETS IGUAL BRACK2D:d FINCADENA    {: RESULT = new Vector(a, b, d,TipoEDD.VECTOR, new Tipo(c), aleft,aright); :}
            |MUTABILIDAD:a ID:b DOS_PUNTOS TIPO:c BRACKETS BRACKETS IGUAL BRACKET_A BRACK2D:d BRACKET_C FINCADENA {: RESULT = new Vector(a, b, d,TipoEDD.VECTOR2D, new Tipo(c), aleft,aright); :}
;

ANIADIR ::= ID:a DOTT APEL PAR_A EXPRESION:b PAR_C FINCADENA    {: RESULT = new ApenList(a, b, aleft, aright);  :}
;

ASIGNACIONV ::= ID:a BRACKET_A EXPRESION:b BRACKET_C IGUAL EXPRESION:c FINCADENA         {: RESULT = new AsignarVec(a,b,c,aleft,aright);:}
                |ID:a BRACKET_A EXPRESION:b BRACKET_C BRACKET_A EXPRESION:d BRACKET_C IGUAL EXPRESION:c FINCADENA         {: RESULT = new AsignarVec(a,b, d,c,aleft,aright);:}
                |ID:a BRACKET_A EXPRESION:b BRACKET_C IGUAL BRACK2D:c FINCADENA         {: RESULT = new AsignarVec(a,b,c,aleft,aright);:}
;

BRACK2D ::= BRACK:a COMA BRACK2D:b                      {: RESULT = b; RESULT.add(a); :}
        |BRACK:a                                        {: RESULT =new LinkedList<>(); RESULT.add(a); :}
;

BRACK ::= BRACKET_A VALORES:a BRACKET_C                 {: RESULT = a; :}
;

VALORES ::= EXPRESION:a COMA VALORES:b                  {:  RESULT = b; RESULT.add(a);   :}
            |EXPRESION:a                                {: RESULT = new LinkedList<>(); RESULT.add(a);  :}
;


CMATCH::= MATCH EXPRESION:a LLAVE_A CASOS:b LLAVE_C                             {: RESULT = new Match(a,b,aleft,aright); :}
;

CASOS ::=   EXPRESION:a FLECHA LLAVE_A INSTRUCCIONES:b LLAVE_C CASOS:c            {: RESULT = new LinkedList<>(); RESULT.add(new Caso(a,b,aleft,aright)); RESULT.addAll(c); :}
            |EXPRESION:a FLECHA LLAVE_A INSTRUCCIONES:b LLAVE_C                    {: RESULT = new LinkedList<>(); RESULT.add(new Caso(a,b,aleft,aright)); :}
            |DEFAULT FLECHA LLAVE_A INSTRUCCIONES:a LLAVE_C                        {: RESULT = new LinkedList<>(); RESULT.add(new Caso(a,aleft,aright)); :}
;

FINB ::= FIN:a FINCADENA                        {: RESULT= new Fin(aleft,aright); :}
            |CONTINUAR:a FINCADENA              {: RESULT = new Fin(true, aleft, aright);:}
;

DOWHILE ::= DO_C LLAVE_A INSTRUCCIONES:a LLAVE_C WHILEC PAR_A EXPRESION:b PAR_C FINCADENA           {: RESULT = new Dow(a,b,aleft,aright); :}
;

CWHILE ::= WHILEC PAR_A EXPRESION:a PAR_C LLAVE_A INSTRUCCIONES:b LLAVE_C                           {: RESULT = new WhileC(a,b,aleft,aright); :}  
;

FORB::= FORC PAR_A ASIG:a EXPRESION:b FINCADENA ACTU_FOR:c PAR_C LLAVE_A INSTRUCCIONES:d LLAVE_C    {:RESULT = new ForC(a,b,c,d,aleft,aright);:}
;

ASIG ::= ASIGNACION:a           {:RESULT = a;:}
    |DECLARACION:a            {:RESULT = a; :}
;

ASIGNACION ::= ID:a IGUAL EXPRESION:b FINCADENA         {: RESULT = new AsignacionVar(a,b,aleft,aright);:}
            |ID:a IGUAL OPERACIONU:b                    {: RESULT = new AsignacionVar(a,b,aleft,aright);:}
;

DECLARACION ::= MUTABILIDAD:a ID:b DOS_PUNTOS TIPO:c IGUAL EXPRESION:d FINCADENA        {: RESULT =new Declaracion(true,a,b,d,new Tipo(c),aleft,aright); :}
                |MUTABILIDAD:a ID:b DOS_PUNTOS TIPO:c FINCADENA                         {: RESULT = new Declaracion(false,a,b,new Tipo(c),aleft,aright); :}
;  

OPERACIONU ::= ID:a INCREMENTO FINCADENA                {: RESULT = new OperacionUnitaria(a,true,aleft,aright); :}
                |ID:a DECREMENTO FINCADENA              {: RESULT = new OperacionUnitaria(a,false,aleft,aright); :}
;

ACTU_FOR ::= ID:a IGUAL EXPRESION:b         {: RESULT = new AsignacionVar(a,b,aleft,aright);:}
            |ID:a INCREMENTO                {: RESULT = new OperacionUnitaria(a,true,aleft,aright); :}
            |ID:a DECREMENTO                {: RESULT = new OperacionUnitaria(a,false,aleft,aright); :}
;
SENT_IF::= IF PAR_A EXPRESION:a PAR_C LLAVE_A INSTRUCCIONES:b LLAVE_C                       {: RESULT = new IF(a, b,aleft,aright); :}
            |IF PAR_A EXPRESION:a PAR_C LLAVE_A INSTRUCCIONES:b LLAVE_C BUCLE_IF:c          {:RESULT = new IF(a,b,(IF)c, aleft,aright);:}
            | IF PAR_A EXPRESION:a PAR_C LLAVE_A INSTRUCCIONES:b LLAVE_C ELSE LLAVE_A INSTRUCCIONES:c LLAVE_C   {:RESULT = new IF(a,b, new Else(c,aleft,aright),aleft,aright);:}
;

BUCLE_IF::= ELSE SENT_IF:a                                                              {:RESULT = a;:}
;

IMPRESION ::= IMPRIMIR PAR_A EXPRESION:a PAR_C FINCADENA                                {:   RESULT = new Print(a, aleft, aright);   :}
;

EXPRESION ::= NOT EXPRESION:a                           {:   RESULT = new Logicos(OperadoresLogicos.NOT , a, aleft, aright);   :}
            | EXPRESION:a XOR EXPRESION:b               {:   RESULT = new Logicos(a, b, OperadoresLogicos.XOR , aleft, aright);   :}
            | EXPRESION:a AND EXPRESION:b               {:   RESULT = new Logicos(a, b, OperadoresLogicos.AND , aleft, aright);   :}
            | EXPRESION:a OR EXPRESION:b                {:   RESULT = new Logicos(a, b, OperadoresLogicos.OR , aleft, aright);   :}
            | EXPRESION:a MENOR EXPRESION:b             {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MENOR , aleft, aright);   :}
            | EXPRESION:a MAYOR EXPRESION:b             {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MAYOR , aleft, aright);   :}
            | EXPRESION:a MENOR_IGUAL EXPRESION:b       {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MENOR_IGUAL , aleft, aright);   :}
            | EXPRESION:a MAYOR_IGUAL EXPRESION:b       {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.MAYOR_IGUAL , aleft, aright);   :}
            | EXPRESION:a DIFERENCIA EXPRESION:b        {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.DIFERENCIA, aleft, aright);   :}
            | EXPRESION:a IGUALACION EXPRESION:b        {:   RESULT = new Relacionales(a, b, OperadoresRelacionales.IGUALACION, aleft, aright);   :}
            | EXPRESION:a MENOS EXPRESION:b             {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.RESTA, aleft, aright);   :}
            | EXPRESION:a MAS EXPRESION:b               {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.SUMA, aleft, aright);   :}
            | EXPRESION:a MODULO EXPRESION:b            {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MODULO, aleft, aright);   :}
            | EXPRESION:a POTENCIA EXPRESION:b          {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.POTENCIA, aleft, aright);   :}
            | EXPRESION:a DIV EXPRESION:b               {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.DIVISION, aleft, aright);   :}
            | EXPRESION:a MULT EXPRESION:b              {:   RESULT = new Aritmeticas(a, b, OperadoresAritmeticos.MULTIPLICACION, aleft, aright);   :}
            | MENOS EXPRESION:a                         {:   RESULT = new Aritmeticas(a, OperadoresAritmeticos.NEGACION, aleft, aright);   :} %prec UMENOS          
            | ENTERO:a                                  {:   RESULT = new Nativo(Integer.parseInt(a), new Tipo(TipoDato.ENTERO), aleft, aright );   :}
            | DECIMAL:a                                 {:   RESULT = new Nativo(Double.parseDouble(a), new Tipo(TipoDato.DECIMAL), aleft, aright );   :}
            | CADENA:a                                  {:   RESULT = new Nativo(a, new Tipo(TipoDato.CADENA), aleft, aright );   :}
            | BOOL:a                                    {:   RESULT = new Nativo(Boolean.parseBoolean(a), new Tipo(TipoDato.BOOL), aleft, aright );   :}
            | LLAMADA:a                                 {:RESULT =a;:}
            | ID:a BRACKET_A EXPRESION:b BRACKET_C       {:   RESULT = new AccesoVec(a, b,aleft, aright );   :}
            | ID:a BRACKET_A EXPRESION:b BRACKET_C BRACKET_A EXPRESION:c BRACKET_C       {:   RESULT = new AccesoVec(a, b,c, aleft, aright );   :}
            | ID:a DOTT REST PAR_A EXPRESION:b PAR_C        {: RESULT = new RestList(a,b, aleft, aright);:}
            | CARACTER:a                                {:   RESULT = new Nativo(a, new Tipo(TipoDato.CARACTER), aleft, aright );   :}
            | PAR_A TIPO:a PAR_C EXPRESION:b            {: RESULT = new Casteo(a, b, aleft, aright); :}   %prec UMENOS         
            | PAR_A EXPRESION:a PAR_C                   {: RESULT=a; :}  
            | ID:a                                      {:   RESULT = new AccesoVar(a, aleft, aright );   :}
;


TIPO::= INT             {:  RESULT = TipoDato.ENTERO;  :}
        |DOUBLE         {:  RESULT = TipoDato.DECIMAL;  :}
        |CHAR           {:  RESULT = TipoDato.CARACTER;  :}
        |STRING         {: RESULT = TipoDato.CADENA; :}
        |BOOLEAN        {: RESULT = TipoDato.BOOL; :}
        |TVOID           {:RESULT = TipoDato.VOID;:}
;

MUTABILIDAD ::= VAR         {:  RESULT = TipoMutable.VAR;  :}
                |CONST      {:  RESULT = TipoMutable.CONST;  :}
;
